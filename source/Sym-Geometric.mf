
beginsymbol(2dot_size# + 2side_bearing#, vcentre(2dot_size#)); "centre dot";
  pickup rule_pen;

  fill circle((w/2, (h-d)/2), dot_size);
endchar;

for alpha = 0, 90, 45, 135:
  beginarrow(alpha, (4dot_size# + equal_spread#)/arrow_horiz_len#, 2dot_size#);
    "colon (" & decimal alpha & ")";
    pickup rule_pen;

    z1 - z0 = (2dot_size + equal_spread) * dir alpha;
    1/2[z0,z1] = centre;

    fill circle(z0, dot_size);
    fill circle(z1, dot_size);
  endchar;
endfor;

for alpha = 0, 90, 45, 135:
  beginarrow(alpha, (6dot_size# + 2equal_spread#)/arrow_horiz_len#, 2dot_size#);
    "triple colon (" & decimal alpha & ")";
    pickup rule_pen;

    z1 - z0 = (2dot_size + equal_spread) * dir alpha;
    z2 - z1 = (2dot_size + equal_spread) * dir alpha;
    z1      = centre;

    fill circle(z0, dot_size);
    fill circle(z1, dot_size);
    fill circle(z2, dot_size);
  endchar;
endfor;

for alpha = 0, 90, 180, 270:
  beginoperator(plus_size#); "therefore (" & decimal alpha & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;
    r := r * 1/3[1, sqrt 2];

    fill circle(centre + r * dir alpha,         dot_size);
    fill circle(centre + r * dir (alpha + 120), dot_size);
    fill circle(centre + r * dir (alpha + 240), dot_size);
  endchar;
endfor;

for alpha = 0, 45:
  beginoperator(plus_size#); "quad dot (" & decimal alpha & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;
    r := r * 1/3[1, sqrt 2];

    fill circle(centre + r * dir alpha,         dot_size);
    fill circle(centre + r * dir (alpha +  90), dot_size);
    fill circle(centre + r * dir (alpha + 180), dot_size);
    fill circle(centre + r * dir (alpha + 270), dot_size);
  endchar;
endfor;

beginoperator(plus_size#); "five dots";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;
  r := r * 1/3[1, sqrt 2];

  fill circle(centre, dot_size);
  fill circle(centre + r * dir   0, dot_size);
  fill circle(centre + r * dir  90, dot_size);
  fill circle(centre + r * dir 180, dot_size);
  fill circle(centre + r * dir 270, dot_size);
endchar;

beginoperator(plus_size#); "minus";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir 0) -- (centre + r * dir 180);
endchar;

beginoperator(plus_size#); "bar";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir 90) -- (centre + r * dir 270);
endchar;

beginoperator(plus_size#); "medium slash";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;
  r := r * 1/3[1, sqrt 2];

  draw (centre + r * dir  45) -- (centre + r * dir 225);
endchar;

beginoperator(plus_size#); "medium backslash";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;
  r := r * 1/3[1, sqrt 2];

  draw (centre + r * dir 135) -- (centre + r * dir 315);
endchar;

beginoperator(plus_size#); "plus";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir  0) -- (centre + r * dir 180);
  draw (centre + r * dir 90) -- (centre + r * dir 270);
endchar;

beginoperator(plus_size#); "times";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;
  r := r * 1/3[1, sqrt 2];

  draw (centre + r * dir  45) -- (centre + r * dir 225);
  draw (centre + r * dir 135) -- (centre + r * dir 315);
endchar;

beginoperator(plus_size#); "plus minus";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir  0) -- (centre + r * dir 180);
  draw (centre + r * dir 90) -- (centre + r * dir 270);

  draw (centre + r * (dir 0 - dir 90)) -- (centre + r * (dir 180 - dir 90));
endchar;

beginoperator(plus_size#); "minus plus";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir  0) -- (centre + r * dir 180);
  draw (centre + r * dir 90) -- (centre + r * dir 270);

  draw (centre + r * (dir 0 + dir 90)) -- (centre + r * (dir 180 + dir 90));
endchar;

for angle = 0, 90, 180, 270:
  beginoperator(plus_size#); "dot minus (" & decimal angle & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;

    draw (centre - r * dir angle) -- (centre + r * dir angle);
    fill circle(centre + equal_spread * dir (angle + 90), dot_size);
  endchar;
endfor;

for angle = 0, 45, 90, 135:
  beginoperator(plus_size#); "divides";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;

    draw (centre - r * dir angle) -- (centre + r * dir angle);
    fill circle(centre + equal_spread * dir (angle + 90), dot_size);
    fill circle(centre - equal_spread * dir (angle + 90), dot_size);
  endchar;
endfor;

beginoperator(plus_size#); "logical not";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir 180) -- (centre + r * dir 0)
                              -- (centre + r * dir 0 - equal_spread * dir 90);
endchar;

beginoperator(plus_size#); "reversed logical not";
  pickup rule_pen;

  r := plus_size/2; % - 11/10rule_thickness;

  draw (centre + r * dir 180 - equal_spread * dir 90)
    -- (centre + r * dir 180) -- (centre + r * dir 0)
endchar;

for alpha = 0, 90, 180, 270:
  beginoperator(plus_size#); "times (" & decimal alpha & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;
    r := r * 1/3[1, sqrt 2];

    draw (centre + r * dir (alpha +  45)) -- (centre + r * dir (alpha + 225)) --
         (centre + r * dir (alpha + 315)) -- (centre + r * dir (alpha + 135));
  endchar;
endfor;

for alpha = 0, 90:
  beginoperator(plus_size#); "bowtie (" & decimal alpha & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;
    r := r * 1/3[1, sqrt 2];

    draw (centre + r * dir (alpha +  45)) -- (centre + r * dir (alpha + 225)) --
         (centre + r * dir (alpha + 135)) -- (centre + r * dir (alpha + 315)) -- cycle;
  endchar;
endfor;

for alpha = 0, 90, 180, 270:
  beginoperator(plus_size#); "Y (" & decimal alpha & ")";
    pickup rule_pen;

    r := plus_size/2; % - 11/10rule_thickness;
    r := r * 1/3[1, sqrt 2];

    draw centre -- (centre + r * dir alpha);
    draw centre -- (centre + r * dir (alpha + 120));
    draw centre -- (centre + r * dir (alpha + 240));
  endchar;
endfor;

% operators

for sign = 1, -1:
  beginoperator(plus_size#); "wedge"; pickup rule_pen;

    z2 - z0 = plus_size * dir 0;

    x1 = 1/2[x0, x2] = w/2;

    y1 - y0 = sign * plus_size;
    1/2[y0,y1] = ypart centre;

    draw z0 -- z1 -- z2;
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "wedge dot";
    pickup rule_pen;

    z2 - z0 = plus_size * dir 0;

    x1 = 1/2[x0, x2] = w/2;

    y1 - y0 = sign * plus_size;
    1/2[y0,y1] = ypart centre;

    draw z0 -- z1 -- z2;
    fill circle(centre - sign * 1/3plus_size * dir 90, dot_size);
  endchar;
endfor;

for sign = 1, -1:
  beginsymbol(13/9plus_size# + 2side_bearing#, vcentre(plus_size# + rule_thickness#));
    "double wedge";

    pickup rule_pen;

    z2 - z0 = plus_size * dir 0;

    x1 = 1/2[x0, x2] = w/2;

    y1 - y0 = sign * plus_size;
    1/2[y0,y1] = ypart centre;

    1/2[z0a,z0b] = z0;
    1/2[z1a,z1b] = z1;
    1/2[z2a,z2b] = z2;
    z0b - z0a = z1b - z1a = z2b - z2a = 4/9plus_size * dir 0;

    draw z0a -- z1a -- z2a;
    draw z0b -- z1b -- z2b;
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "curly wedge";
    pickup rule_pen;

    draw prec(centre + sign * 1/2plus_size * dir 90,
              centre - sign * 1/2plus_size * dir 90,
              plus_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "curly wedge dot";
    pickup rule_pen;

    draw prec(centre + sign * 1/2plus_size * dir 90,
              centre - sign * 1/2plus_size * dir 90,
              plus_size);
    fill circle(centre - sign * 5/12plus_size * dir 90, dot_size);
  endchar;
endfor;

for sign = 1, -1:
  beginsymbol(13/9plus_size# + 2side_bearing#, vcentre(plus_size# + rule_thickness#));
    "double wedge";

    pickup rule_pen;

    1/2[z0,z1] = (w/2, (h-d)/2);
    z1 - z0 = 4/9plus_size * dir 0;

    draw prec(z0 + sign * 1/2plus_size * dir 90,
              z0 - sign * 1/2plus_size * dir 90,
              plus_size);
    draw prec(z1 + sign * 1/2plus_size * dir 90,
              z1 - sign * 1/2plus_size * dir 90,
              plus_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "union";
    pickup rule_pen;

    draw subset(centre - sign * 1/2plus_size * dir 90,
                centre + sign * 1/2plus_size * dir 90,
                plus_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(4/3plus_size#); "double union";
    pickup rule_pen;

    draw subset(centre - sign * 5/18plus_size * dir 90,
                centre + sign * 2/3plus_size * dir 90,
                5/9plus_size);
    draw subset(centre - sign * 2/3plus_size * dir 90,
                centre + sign * 2/3plus_size * dir 90,
                4/3plus_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "union dot";
    pickup rule_pen;

    draw subset(centre - sign * 1/2plus_size * dir 90,
                centre + sign * 1/2plus_size * dir 90,
                plus_size);
    fill circle(centre + sign * 1/6plus_size * dir 90, dot_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "union plus";
    pickup rule_pen;

    draw subset(centre - sign * 1/2plus_size * dir 90,
                centre + sign * 1/2plus_size * dir 90,
                plus_size);

    t := 22/10rule_thickness;
    r := 1/2plus_size - t;

    draw (centre + sign * 1/6plus_size * dir 90 + r * dir  0)
      -- (centre + sign * 1/6plus_size * dir 90 + r * dir 180);
    draw (centre + (r + sign * 1/6plus_size) * dir 90)
      -- (centre + (r - sign * 1/6plus_size) * dir 270);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "square union";
    pickup rule_pen;

    z3 - z0 = z2 - z1 = plus_size * dir 0;
    z0 - z1 = sign * plus_size * dir 90;

    1/2[1/2[z0,z1], 1/2[z2,z3]] = centre;

    draw z0 -- z1 -- z2 -- z3;
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(4/3plus_size#); "double square union";
    pickup rule_pen;

    z3a - z0a = z2a - z1a = 4/3plus_size * dir 0;
    z0a - z1a = sign * 4/3plus_size * dir 90;

    1/2[1/2[z0a,z1a], 1/2[z2a,z3a]] = centre;

    z3b - z0b = z2b - z1b = 5/9plus_size * dir 0;
    z0b - z1b = sign * 17/18plus_size * dir 90;

    y0b = y0a;
    1/2[x0b, x3b] = xpart centre;

    draw z0a -- z1a -- z2a -- z3a;
    draw z0b -- z1b -- z2b -- z3b;
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "square union dot";
    pickup rule_pen;

    z3 - z0 = z2 - z1 = plus_size * dir 0;
    z0 - z1 = sign * plus_size * dir 90;

    1/2[1/2[z0,z1], 1/2[z2,z3]] = centre;

    draw z0 -- z1 -- z2 -- z3;

    fill circle(centre + sign * 1/6plus_size * dir 90, dot_size);
  endchar;
endfor;

for sign = 1, -1:
  beginoperator(plus_size#); "square union plus";
    pickup rule_pen;

    z3 - z0 = z2 - z1 = plus_size * dir 0;
    z0 - z1 = sign * plus_size * dir 90;

    1/2[1/2[z0,z1], 1/2[z2,z3]] = centre;

    draw z0 -- z1 -- z2 -- z3;

    t := 22/10rule_thickness;
    r := 1/2plus_size - t;

    draw (centre + sign * 1/6plus_size * dir 90 + r * dir  0)
      -- (centre + sign * 1/6plus_size * dir 90 + r * dir 180);
    draw (centre + (r + sign * 1/6plus_size) * dir 90)
      -- (centre + (r - sign * 1/6plus_size) * dir 270);
  endchar;
endfor;


% triangles

for alpha = 0, 90, 180, 270:
  beginoperator(small_op_size#); "small triangle (" & decimal alpha & ")";
    pickup rule_pen;
    draw triangle(centre, w/2-side_bearing, alpha);
  endchar;
endfor;

for alpha = 0, 90, 180, 270:
  beginoperator(small_op_size#); "small filled triangle (" & decimal alpha & ")";
    pickup rule_pen;
    draw triangle(centre, w/2-side_bearing, alpha);
    fill triangle(centre, w/2-side_bearing, alpha);
  endchar;
endfor;

for alpha = 0, 90, 180, 270:
  beginoperator(med_op_size#); "medium triangle (" & decimal alpha & ")";
    pickup rule_pen;
    draw triangle(centre, w/2-side_bearing, alpha);
  endchar;
endfor;

for alpha = 0, 90, 180, 270:
  beginoperator(large_op_size#); "large triangle (" & decimal alpha & ")";
    pickup rule_pen;
    draw triangle(centre, w/2-side_bearing, alpha);
  endchar;
endfor;

% circles

beginoperator(4/5small_op_size#); "small circle";
  pickup rule_pen;
  draw circle(centre, 4/10small_op_size);
endchar;

beginoperator(4/5small_op_size#); "small filled circle";
  pickup rule_pen;
  draw circle(centre, 4/10small_op_size);
  fill circle(centre, 4/10small_op_size);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle";
  pickup rule_pen;
  draw circle(centre, plus_size/2 + 11/10rule_thickness);
endchar;

beginoperator(4/5large_op_size#); "large circle";
  pickup rule_pen;
  draw circle(centre, 4/10large_op_size);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle minus";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * left) -- (centre + plus_size/2 * right);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle bar";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * up) -- (centre + plus_size/2 * down);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle slash";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * dir  45) -- (centre + plus_size/2 * dir 225);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle backslash";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * dir 135) -- (centre + plus_size/2 * dir 315);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle plus";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * left) -- (centre + plus_size/2 * right);
  draw (centre + plus_size/2 * up)   -- (centre + plus_size/2 * down);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle times";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + plus_size/2 * dir  45) -- (centre + plus_size/2 * dir 225);
  draw (centre + plus_size/2 * dir 135) -- (centre + plus_size/2 * dir 315);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle dot";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  fill circle(centre, 1.3dot_size);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle circle";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw circle(centre, (plus_size + 11/5rule_thickness)/5);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle triangle";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw triangle(centre, small_op_size/2, 90);
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle asterisk";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);

  penpos a0(4/3rule_thickness,   0 - 7);
  penpos a1(4/3rule_thickness,  72 - 7);
  penpos a2(4/3rule_thickness, 144 - 7);
  penpos a3(4/3rule_thickness, 216 - 7);
  penpos a4(4/3rule_thickness, 288 - 7);

  penpos b0(2/3rule_thickness,   0 - 7);
  penpos b1(2/3rule_thickness,  72 - 7);
  penpos b2(2/3rule_thickness, 144 - 7);
  penpos b3(2/3rule_thickness, 216 - 7);
  penpos b4(2/3rule_thickness, 288 - 7);

  reg_poly_points(a)(5, centre, plus_size/2, 77);

  z.b0 = z.b1 = z.b2 = z.b3 = z.b4 = centre;

  fill stroke z.a0e -- z.b0e;
  fill stroke z.a1e -- z.b1e;
  fill stroke z.a2e -- z.b2e;
  fill stroke z.a3e -- z.b3e;
  fill stroke z.a4e -- z.b4e;
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "medium circle star";
  pickup rule_pen;

  draw circle(centre, plus_size/2 + 11/10rule_thickness);

  reg_poly_points(a)(5, centre, plus_size/2, 90);
  reg_poly_points(b)(5, centre, plus_size/20, 126);

  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
  fill z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;

beginoperator(plus_size# + 11/5rule_thickness#); "negated medium circle";
  pickup rule_pen;

  r := 3/4(plus_size + 11/5rule_thickness);

  draw circle(centre, plus_size/2 + 11/10rule_thickness);
  draw (centre + r * dir 45) -- (centre + r * dir 225);
endchar;

% squares

beginoperator(small_op_size#/sqrt(2)); "small square";
  pickup rule_pen;
  draw square(centre, w/2 - side_bearing, 0);
endchar;
beginoperator(small_op_size#/sqrt(2)); "small filled square";
  pickup rule_pen;
  draw square(centre, w/2 - side_bearing, 0);
  fill square(centre, w/2 - side_bearing, 0);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square";
  pickup rule_pen;
  draw square(centre, w/2 - side_bearing, 0);
endchar;
beginoperator(large_op_size#/sqrt(2)); "large square";
  pickup rule_pen;
  draw square(centre, w/2 - side_bearing, 0);
endchar;

beginoperator(small_op_size#); "small diamond";
  pickup rule_pen;
  draw square(centre, (w/2 - side_bearing) / sqrt(2), 45);
endchar;
beginoperator(small_op_size#); "small filled diamond";
  pickup rule_pen;
  draw square(centre, (w/2 - side_bearing) / sqrt(2), 45);
  fill square(centre, (w/2 - side_bearing) / sqrt(2), 45);
endchar;
beginoperator(med_op_size#); "medium diamond";
  pickup rule_pen;
  draw square(centre, (w/2 - side_bearing) / sqrt(2), 45);
endchar;
beginoperator(large_op_size#); "large diamond";
  pickup rule_pen;
  draw square(centre, (w/2 - side_bearing) / sqrt(2), 45);
endchar;

beginoperator(med_op_size#/sqrt(2)); "medium square minus";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + sqrt .5 * r * dir  0) -- (centre + sqrt .5 * r * dir 180);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square vert";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + sqrt .5 * r * dir 90) -- (centre + sqrt .5 * r * dir 270);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square slash";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + r * dir 45) -- (centre + r * dir 225);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square backslash";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + r * dir 135) -- (centre + r * dir 315);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square plus";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + sqrt .5 * r * dir  0) -- (centre + sqrt .5 * r * dir 180);
  draw (centre + sqrt .5 * r * dir 90) -- (centre + sqrt .5 * r * dir 270);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square times";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw (centre + r * dir  45) -- (centre + r * dir 225);
  draw (centre + r * dir 135) -- (centre + r * dir 315);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square dot";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  fill circle(centre, 1.3dot_size);
endchar;
beginoperator(med_op_size#/sqrt(2)); "medium square square";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r, 0);
  draw square(centre, .4r, 0);
endchar;

beginoperator(med_op_size#); "medium diamond minus";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + r * dir  0) -- (centre + r * dir 180);
endchar;
beginoperator(med_op_size#); "medium diamond vert";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + r * dir 90) -- (centre + r * dir 270);
endchar;
beginoperator(med_op_size#); "medium diamond slash";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + sqrt .5 * r * dir 45) -- (centre + sqrt .5 * r * dir 225);
endchar;
beginoperator(med_op_size#); "medium diamond backslash";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + sqrt .5 * r * dir 135) -- (centre + sqrt .5 * r * dir 315);
endchar;
beginoperator(med_op_size#); "medium diamond plus";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + r * dir  0) -- (centre + r * dir 180);
  draw (centre + r * dir 90) -- (centre + r * dir 270);
endchar;
beginoperator(med_op_size#); "medium diamond times";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw (centre + sqrt .5 * r * dir  45) -- (centre + sqrt .5 * r * dir 225);
  draw (centre + sqrt .5 * r * dir 135) -- (centre + sqrt .5 * r * dir 315);
endchar;
beginoperator(med_op_size#); "medium diamond dot";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  fill circle(centre, 1.3dot_size);
endchar;
beginoperator(med_op_size#); "medium diamond diamond";
  pickup rule_pen;
  r := w/2 - side_bearing;
  draw square(centre, r / sqrt(2), 45);
  draw square(centre, .4r, 45);
endchar;

beginoperator(small_op_size#); "small star";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  reg_poly_points(b)(5, centre, .4r, 126);
  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;
beginoperator(small_op_size#); "small filled star";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  reg_poly_points(b)(5, centre, .4r, 126);
  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
  fill z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;
beginoperator(med_op_size#); "medium star";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  reg_poly_points(b)(5, centre, .4r, 126);
  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;
beginoperator(large_op_size#); "large star";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  reg_poly_points(b)(5, centre, .4r, 126);
  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;
beginoperator(med_op_size#); "medium pentagram";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  draw z.a0 -- z.a2 -- z.a4 -- z.a1 -- z.a3 -- cycle;
endchar;
beginoperator(large_op_size#); "large pentagram";
  pickup rule_pen;
  r := w/2 - side_bearing;
  reg_poly_points(a)(5, centre, r, 90);
  draw z.a0 -- z.a2 -- z.a4 -- z.a1 -- z.a3 -- cycle;
endchar;

beginoperator(small_op_size#); "medium thin filled star";
  pickup rule_pen;
  pickup currentpen scaled 1/2;

  r := w/2 - side_bearing;

  reg_poly_points(a)(5, centre, r, 90);
  reg_poly_points(b)(5, centre, .2r, 126);

  draw z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
  fill z.a0 -- z.b0 -- z.a1 -- z.b1 -- z.a2 -- z.b2 -- z.a3 -- z.b3 -- z.a4 -- z.b4 -- cycle;
endchar;

beginoperator(small_op_size#); "asterisk";
  r := w/2 - side_bearing;

  penpos a0(4/3rule_thickness,   0 - 7);
  penpos a1(4/3rule_thickness,  72 - 7);
  penpos a2(4/3rule_thickness, 144 - 7);
  penpos a3(4/3rule_thickness, 216 - 7);
  penpos a4(4/3rule_thickness, 288 - 7);

  penpos b0(2/3rule_thickness,   0 - 7);
  penpos b1(2/3rule_thickness,  72 - 7);
  penpos b2(2/3rule_thickness, 144 - 7);
  penpos b3(2/3rule_thickness, 216 - 7);
  penpos b4(2/3rule_thickness, 288 - 7);

  reg_poly_points(a)(5, centre, r, 77);

  z.b0 = z.b1 = z.b2 = z.b3 = z.b4 = centre;

  fill stroke z.a0e -- z.b0e;
  fill stroke z.a1e -- z.b1e;
  fill stroke z.a2e -- z.b2e;
  fill stroke z.a3e -- z.b3e;
  fill stroke z.a4e -- z.b4e;
endchar;

beginoperator(med_op_size#); "between";
  pickup rule_pen;

  r := w/2 - side_bearing;

  draw smile( 1, centre - r * dir 90, centre + r * dir 90, equal_spread, true);
  draw smile(-1, centre - r * dir 90, centre + r * dir 90, equal_spread, true);
endchar;

beginoperator(med_op_size#); "reversed between";
  pickup rule_pen;

  r := w/2 - side_bearing;

  draw smile( 1,  centre - 2/3equal_spread * dir 0 - r * dir 90,
                  centre - 2/3equal_spread * dir 0 + r * dir 90,
                  2/3equal_spread, true);
  draw smile(-1, centre + 2/3equal_spread * dir 0 - r * dir 90,
                 centre + 2/3equal_spread * dir 0 + r * dir 90,
                 2/3equal_spread, true);
endchar;

for sign = 1, -1:
  beginsymbol(order_width# + 2side_bearing#, 2math_axis# + rule_thickness#, rule_thickness#); "top";
    pickup rule_pen;

    z1 - z0 = z3 - z2 = order_width * dir 0;
    z2 - z0 = sign * 2math_axis * dir 90;

    1/2[1/2[z0,z1], 1/2[z2,z3]] = (w/2, (h-d)/2);

    draw z0 -- z1;
    draw 1/2[z0,z1] -- 1/2[z2,z3];
  endchar;
endfor;

beginsymbol(2/3equal_spread# + 2side_bearing#, vcentre(plus_size#+rule_thickness#)); "wreath";
  pickup rule_pen;

  z0 = (w/2, (h-d)/2);

  draw sim(z0 + 1/2plus_size * dir 90, z0 - 1/2plus_size * dir 90);
endchar;

beginoperator(med_op_size#); "angle";
  pickup rule_pen;

  z0 = centre - 1/3plus_size * dir 90;

  z1 = z0 + 1/2plus_size * dir 0;
  z2 = z0 - 1/2plus_size * dir 0;
  x3 = xpart centre + 1/2plus_size;
  y3 = ypart centre + 2/3plus_size;

  draw z1 -- z2 -- 5/6[z2,z3];
endchar;

beginoperator(med_op_size#); "measured angle";
  pickup rule_pen;

  z0 = centre - 1/3plus_size * dir 90;

  z1 = z0 + 1/2plus_size * dir 0;
  z2 = z0 - 1/2plus_size * dir 0;
  x3 = xpart centre + 1/2plus_size;
  y3 = ypart centre + 2/3plus_size;

  z4 = 2/3 [z2,z1];
  z5 = z2 + (z4 - z2) rotated angle (z3 - z2);

  draw z1 -- z2 -- 5/6[z2,z3];
  draw z4{dir 90} .. {dir (angle (z3 - z2) + 90)}z5;
endchar;

beginoperator(med_op_size#); "spherical angle";
  pickup rule_pen;

  z0 = centre - 1/2plus_size * dir 0;

  z1 = z0 + plus_size * dir 25;
  z2 = z0 + plus_size * dir -25;
  z3 = z0 + 2/3plus_size * dir 25;
  z4 = z0 + 2/3plus_size * dir -25;

  draw z1 -- z0 -- z2;
  draw z4{dir 65} .. {dir 115}z3;
endchar;

beginsymbol(5.5u#, 4/5asc_height#, 0); "prime";
  pickup penrazor;

  z0 = (w, h);

  z1 = z0 + 7/3u * dir 183;

  z2 = (1u, 1/9h);
  z3 = z2 + u * dir -20;

  fill z0 -- z1 -- z2 -- z3 -- cycle;
endchar;

beginsymbol(5.5u#, 4/5asc_height#, 0); "backprime";
  pickup penrazor;

  z0 = (0, h);

  z1 = z0 + 7/3u * dir -3;

  z2 = (w - 1u, 1/9h);
  z3 = z2 + u * dir 200;

  fill z0 -- z1 -- z2 -- z3 -- cycle;
endchar;

beginsymbol(2order_width# + 2side_bearing#, vcentre(med_op_size#)); "infinity";
  pickup rule_pen;

  lft x0 = side_bearing;
  rt  x1 = w - side_bearing;

  y0 = y1 = (h-d)/2;

  x2 = x3 = 1/5[x0,x1];
  x4 = x5 = 4/5[x0,x1];

  y2 = y4 = y0 + 5/6equal_spread;
  y3 = y5 = y0 - 5/6equal_spread;

  draw z4{dir 180} .. {dir 225}1/2[z0,z1]{dir 225} .. {dir 180}z3{dir 180}
    .. {dir 90}z0{dir 90} .. {dir 0}z2{dir 0} .. {dir -45}1/2[z0,z1]{dir -45} .. {dir 0}z5{dir 0}
    .. {dir 90}z1{dir 90} .. cycle;
endchar;

beginsymbol(17/10order_width# + 2side_bearing#, vcentre(med_op_size#)); "proportional";
  pickup rule_pen;

  lft x0 = side_bearing;
  rt x6 = rt x7 = w - side_bearing;
  x4 = x5 = x6 - 1/10order_width;

  y0 = (h-d)/2;

  z1 = 1/2[z4,z5];

  x2 = x3 = 1/4[x0,x1];

  y2 = y4 = y6 = y0 + 5/6equal_spread;
  y3 = y5 = y7 = y0 - 5/6equal_spread;

  draw z6 -- z4{dir 180} .. {dir 225}5/8[z0,z1]{dir 225} .. {dir 180}z3{dir 180}
    .. {dir 90}z0{dir 90} .. {dir 0}z2{dir 0} .. {dir -45}5/8[z0,z1]{dir -45} .. {dir 0}z5 -- z7;
endchar;

for sign = 1, -1:
  beginoperator(plus_size#); "product";
    draw_product(centre, plus_size, plus_size, sign, rule_thickness);
  endchar;
endfor;

beginsymbol(13u# + 2side_bearing#, asc_height#, 0); "check mark";
  pickup pencircle
    xscaled 3/2rule_thickness
    yscaled 3/4rule_thickness
    rotated 30;

  lft x1 = hround(1.5u);
  x2 = hround(w/3);
  x3 = w - x1;
  x4 = w - 3/2x2;

  y1 = math_axis;
  y2 = -d;
  y3 = y4 = h;

  draw z1 -- z2{z4 - z2} .. z3;

  penlabels(1,2,3,4);
endchar; 

beginsymbol(6u# + 2side_bearing#, 5/4asc_height#, 1/8asc_height#); "lightning";

  penpos1 (2rule_thickness, -20);
  penpos4 (5/6rule_thickness, -20);

  x3r = w - side_bearing;
  x2l = side_bearing;
  x4  = 1/4w;

  y1l = h;
  y3r = 14/24h;
  y4  = -d + 1/2rule_thickness;

  z2'l = z2l;
  z3'r = z3r;

  z3'l = z2l + whatever * dir 15;
  z2'r = z3r + whatever * dir 15;

  z2r - z2l = 3/2rule_thickness * dir 15;
  z3r - z3l = 2rule_thickness * dir 15;
  z3'l = z3r + rule_thickness * dir (angle (z4r - z3r));

  z1l - z2l = whatever * (z4r - z3r);
  z2'r = whatever[z1l,z2l];

  fill stroke z1e -- z2e -- z2'e -- z3'e -- z3e -- z4e;

  pickup rule_pen;

  draw arrowhead(z4, angle (z4l - z3l), 4/5arrow_spread);

  penlabels(1,2,2',3',3,4);
endchar;

beginsymbol(11u#, 1/2[x_height#,asc_height#], 0)
  "diamond suit";

  x0 = x2 = w/2;
  1/2[y0,y2] = h/2;
  y2 - y0 = h;

  1/2[x1,x3] = w/2;
  x3 - x1 = w - 2side_bearing;
  y1 = y3 = h/2;

  pickup pencircle scaled rule_thickness;

  draw z0{dir (90+30)} .. {dir (180-30)}z1{dir (0+30)}
                       .. {dir (90-30)}z2{dir (270+30)}
                       .. {dir (0-30)}z3{dir (180+30)}
                       .. {dir (270-30)}z0;
endchar;

beginsymbol(11u#, 1/2[x_height#,asc_height#], 0)
  "heart suit";

  x0 = x1 = w/2;
  y0 = 0;

  1/2[x2,x3] = w/2;
  x3 - x2 = w - 2side_bearing;

  y1 = y2 = y3;

  x4 = 1/2[x1,x2];
  x5 = 1/2[x1,x3];

  y4 = y5 = h;

  h - y1 = x4 - x2;

  pickup pencircle scaled rule_thickness;

  draw z0{dir (90+20)} .. z2 .. z4 .. {dir (270+20)}z1{dir (90-20)} .. z5 .. z3 .. {dir (270-20)}z0;
endchar;

beginsymbol(10u#, 1/2[x_height#,asc_height#], 0)
  "spade suit";

  x0 = x1 = w/2;
  y0 = h + 1/2rule_thickness;

  1/2[x2,x3] = w/2;
  x3 - x2 = w - 2side_bearing;

  y2 = y3;

  x4 = 1/2[x1,x2];
  x5 = 1/2[x1,x3];

  y4 = y5 = 1/9h;

  y2 - y4 = x4 - x2;

  y1 = 1/3[y2,y4];

  x6 = x0;
  y6 = 0;
  penpos1(1/2rule_thickness, 0);
  penpos6(3/2rule_thickness, 0);

  pickup pencircle scaled rule_thickness;

  fill z0{dir (270-20)} .. z2 .. z4 .. {dir (90-20)}(z1 + 1/2rule_thickness*left)
       -- (z1 + 1/2rule_thickness*right){dir (270+20)} .. z5 .. z3 .. {dir (90+20)}z0 -- cycle;

  fill stroke z1e -- z6e;
endchar;

beginsymbol(11u#, 1/2[x_height#,asc_height#], 0)
  "club suit";

  radius := (w - 4side_bearing)/4;

  x0 = w/2;
  y0 = h - 3radius;

  x1 = w - x3 = x0/2;
  y1 = y3 = y0;
  x2 = x4 = x0;
  y2 = h - radius;
  y4 = 0;

  penpos0(2/3rule_thickness,  0);
  penpos1(2/3rule_thickness, 90);
  penpos2(2/3rule_thickness,  0);
  penpos3(2/3rule_thickness, 90);
  penpos4(3/2rule_thickness,  0);

  fill circle(z1, radius);
  fill circle(z2, radius);
  fill circle(z3, radius);

  fill stroke z2e -- z0e{down} .. z4e;
  fill stroke z1e -- z3e;
endchar;

beginsymbol(5/4order_width# + 2side_bearing#, asc_height#, 0); "forall";
  pickup rule_pen;

  lft x0 = side_bearing;
  rt x1  = w - side_bearing;
  x2     = w/2;

  top y0 = top y1 = h;
  bot y2 = 0;

  z4 = whatever [z0,z2];
  z5 = whatever [z1,z2];

  y4 = y5 = x_height;

  draw z0 -- z2 -- z1;
  draw z4 -- z5;
endchar;

for negated = 0, 1:
  beginsymbol(order_width# + 2side_bearing#,
              if negated = 0: asc_height# else: asc_height# + 2/3equal_spread# fi,
              if negated = 0: 0 else: 2/3equal_spread# fi);
    "exists";

    pickup rule_pen;

    lft x0 = lft x2 = side_bearing;
    rt x1  = rt x3  = w - side_bearing;

    top y0 = top y1 = asc_height;
    bot y2 = bot y3 = 0;

    z4 = 1/8[1/2[z0,z2], 1/2[z1,z3]];

    draw z0 -- z1 -- z3 -- z2;
    draw z4 -- 1/2[z1,z3];

    if negated = 1:
      stroke_through(1/2[1/2[z0,z2], 1/2[z1,z3]], h + d - stroke_through_thickness)
    fi;
  endchar;
endfor;

beginsymbol(3/2order_width# + 2side_bearing#, vcentre(med_op_size#)); "nabla";
  pickup rule_pen;
  pickup currentpen scaled 2/3;

  top y0 = top y1 = h;
  bot y2 = -d;

  x0 = side_bearing;
  x1 = w - side_bearing;
  x2 = w/2;

  y3 = y4 = y0 - 2/3rule_thickness + eps;

  z3 = whatever[z0,z2];
  z4 = whatever[z1,z2];
  z5 = whatever[z0,z1];
  z6 = whatever[z1,z2];

  x5 - x0 = -2/3rule_thickness / sind (angle (z2 - z0));

  z6 = z5 + whatever * (z2 - z0);

  draw z0 -- z1 -- z2 -- cycle;
  draw z3 -- z4;
  draw z5 -- z6;
endchar;

beginsymbol(1/2order_width# + 2side_bearing#,
            asc_height# + 2/3rule_thickness#,
            2/3rule_thickness#);
  "small integral";

  pair centre;

  centre := (w/2, (h - d)/2);

  draw_integral([0])(1/2, centre);
endchar;

beginsymbol(order_width#, asc_height#, 0)
  "flat";

  x0 = x1 = x4 = side_bearing + rule_thickness;
  y0 = 0; y1 = h;
  x3 = w - side_bearing;

  y3 = 0.45[y0,y1];

  y4 = 4/5x_height;

  penpos0(4/5rule_thickness, 10);
  penpos1(4/5rule_thickness, 10);
  penpos3(4/5rule_thickness, 0);

  fill stroke z0e -- z1e;

  pickup pencircle scaled 4/5rule_thickness;

  draw z0{dir 40} .. {up}z3 .. {dir 230}z4;

  penlabels (0,1,3,4);
endchar;

beginsymbol(order_width#, asc_height#, 0)
  "natural";

  x0 = x1 = w - x2 = w - x3 = side_bearing + 1/2rule_thickness;
  x4 = x5 = w - x6 = w - x7 = side_bearing;

  y1 = y3 = h;
  y7 = x_height;
  y6l = 0;
  y0 = y2  = -h/3;

  z6 = z4 + whatever * dir 15;
  z5 = z7 + whatever * dir 15;

  z8 = whatever[z0,z1] = whatever[z4,z6];
  z9 = whatever[z2,z3] = whatever[z5,z7];

  x10 = x0l;
  x11 = x0l;
  x12 = x2r;
  x13 = x2r;

  z10 = whatever[z4,z6];
  z11 = whatever[z5,z7];
  z12 = whatever[z4,z6];
  z13 = whatever[z5,z7];

  penpos0(4/5rule_thickness, 10);
  penpos1(4/5rule_thickness, 10);
  penpos2(4/5rule_thickness, 10);
  penpos3(4/5rule_thickness, 10);
  penpos4(5/4rule_thickness, 90);
  penpos5(5/4rule_thickness, 90);
  penpos6(5/4rule_thickness, 90);
  penpos7(5/4rule_thickness, 90);
  penpos8 (4/5rule_thickness, 10);
  penpos9 (4/5rule_thickness, 10);
  penpos10(5/4rule_thickness, 90);
  penpos11(5/4rule_thickness, 90);
  penpos12(5/4rule_thickness, 90);
  penpos13(5/4rule_thickness, 90);

  fill stroke z1e -- z8e;
  fill stroke z9e -- z2e;
  fill stroke z10e -- z12e;
  fill stroke z11e -- z13e;

  penlabels (0,1,2,3,4,5,6,7);
endchar;

beginsymbol(order_width#, asc_height#, 0)
  "sharp";

  x0 = x1 = w - x2 = w - x3 = side_bearing + rule_thickness;

  x4 = x5 = w - x6 = w - x7 = side_bearing;

  y3 = h;
  y7 = x_height;
  y6l = 0;
  y0  = -h/3;

  z2 = z0 + whatever * dir 20;
  z1 = z3 + whatever * dir 20;
  z6 = z4 + whatever * dir 15;
  z5 = z7 + whatever * dir 15;

  penpos0(4/5rule_thickness, 10);
  penpos1(4/5rule_thickness, 10);
  penpos2(4/5rule_thickness, 10);
  penpos3(4/5rule_thickness, 10);
  penpos4(5/4rule_thickness, 90);
  penpos5(5/4rule_thickness, 90);
  penpos6(5/4rule_thickness, 90);
  penpos7(5/4rule_thickness, 90);

  fill stroke z0e -- z1e;
  fill stroke z2e -- z3e;
  fill stroke z4e -- z6e;
  fill stroke z5e -- z7e;

  penlabels (0,1,2,3,4,5,6,7);
endchar;

beginsymbol(11u#, 8.5/10asc_height#, 0); "aleph";

  pickup rule_pen;
  pickup currentpen scaled 2/3;

  penpos 1(0, 65);
  penpos 2(rule_thickness, 65);
  penpos 3(rule_thickness, 65);
  penpos 4(0, 65);
  penpos 5(rule_thickness, 65);
  penpos 6(rule_thickness, 65);
  penpos 7(rule_thickness, 65);
  penpos 8(rule_thickness, 65);
  penpos 9(rule_thickness, 65);
  penpos10(rule_thickness, 65);
  penpos11(rule_thickness, 65);
  penpos12(rule_thickness, 65);
  penpos13(rule_thickness, 65);

  x1l = side_bearing;
  x4r = w - side_bearing;
  y1r = h;
  y4l = 0;

  x2 - x1 = x4 - x3 = ceiling (u/2);
  y1 - y2 = y3 - y4 = 2rule_thickness;
  1/2[z2,z3] = (w/2,h/2);

  z5 = 0.24[z2,z3];
  z6 = 0.70[z2,z3];

  x7 = x8 = x2;
  x9 = x5;
  y7 = min (y6, y5 - 3/2rule_thickness);
  bot y8l = bot y9l = 0;

  x10 = x11 = x3;
  y10 = max (y5, y6 + 3/2rule_thickness);
  top y11r = h;

  y13 = y12 = y11;
  x12 = x11 + 2/3rule_thickness;
  x13 = 1/4[x6,x10];

  alpha := angle (z5 - z7);
  penpos5'(3/2rule_thickness * sind (65 - alpha), alpha - 90);
  z5' = z5;

  alpha := angle (z10 - z6);
  penpos6'(3/2rule_thickness * sind (65 - alpha), alpha - 90);
  z6' = z6;

  filldraw stroke z1e{down} ... {z3e-z2e}z2e -- z3e{z3e-z2e} ... {down}z4e;
  filldraw stroke z5'e .. {down}z7e -- z8e;
  filldraw stroke z8e -- z9e;
  filldraw stroke z6'e .. {up}z10e -- z11e;
  filldraw stroke z12e -- z13e;

  labels(1,2,3,4,5,6,7,8,9,10,11);
endchar;

%beginsymbol(11u#, asc_height#, 0); "aleph";
%
%  pickup rule_pen;
%  pickup currentpen scaled 1/2;
%
%%cap_stem := 3/2rule_thickness;
%%cap_hair := 1/2rule_thickness;
%%cap_curve := rule_thickness;
%%stem := 3/2rule_thickness;
%%vair := 1/2rule_thickness;
%
%  penpos1(3/2rule_thickness, 75);
%  penpos2(3/2rule_thickness, 90);
%  penpos3(3/2rule_thickness, 90);
%  penpos4(3/2rule_thickness, 75);
%
%  lft x1l = hround u - eps;
%  x2 = 5/2u = w - x3;
%  rt x4r = hround(w - u) + eps;
%
%  top y1r = h;
%  bot y4l = 0;
%
%  z2 = whatever[z1l, z4r];
%  z3 = whatever[z1l, z4r];
%
%  filldraw z1r{4(x1l - x1r), y1l - y1r} ... {down}z1l ... z2l
%   --- z3l ... {down}z4l{4(x4r - x4l), y4r - y4l} ... {up}z4r ... z3r
%   --- z2r ... {up}cycle; % long diagonal
%
%  penpos5(3/2rule_thickness, 75);
%  penpos6(3/2rule_thickness, 90);
%  penpos7(3/2rule_thickness, 75);
%
%  lft x5l = hround (w - 4u) - eps;
%  x6 = 1/2[x5, x7];
%
%  x7 = x4;
%  y5 = y1;
%  bot y7l = x_height;
%
%  z6 = whatever[z5l, z7r];
%
%  filldraw z5r{4(x5l - x5r), y5l - y5r} ... {down}z5l
%   ... z6l{z7r - z5l} ... {down}z7l{4(x7r - x7l), y7r - y7l} ... {up}z7r
%   ... z6r{z5l - z7l} ... {up}cycle; % short diagonal
%
%  penpos8(1/2rule_thickness, 0);
%  penpos9(1/2rule_thickness, 0);
%  z8 = z6;
%  x9 = x8 - 3/4u;
%  z9 = whatever[z2, z3];
%
%  filldraw z8l{down} .. {down}z9l -- z9r{up} .. {up}z8r -- cycle;  % right stem
%
%  penpos10(1/2rule_thickness, -30);
%  penpos11(rule_thickness, 0);
%  penpos12(3/2rule_thickness, 0);
%  penpos13(rule_thickness, 0);
%  penpos14(1/2rule_thickness, 90);
%
%  lft x11l = hround 3/2u;
%  x10 = x12 = 2/5[x11, w/2];
%  z10 = whatever[z2, z3];
%  lft x13l = hround u;
%
%  z13l = z14l;
%
%  y11 = 1/2y10;
%  y12 = 1/5[y14r, y11];
%  bot y13 = 0;
%
%  filldraw z10l{2(x11 - x10), y11 - y10} ... z11l{down} .. {down}z12l
%        -- z12r{up} .. {up}z11r ... {2(x10 - x11), y10 - y11}z10r -- cycle; % left stem
%  filldraw z12r{down} ... z13r --- z13l -- z14r{right} ... {up}z12l -- cycle; % flourish
%  labels(0l,0r,1l,1r,2l,2r,3l,3r,4l,4r,5l,5r,6l,6r,7l,7r,8l,8r,9l,9r,10l,10r,11l,11r,12l,12r,13l,13r,14l,14r);
%endchar;

beginsymbol(11u#, asc_height#, 0); "beth";

  pickup rule_pen;
  pickup currentpen scaled 1/2;

  top y0 = h;
  lft x0 = u;

  penpos1(3/2rule_thickness, 90);
  penpos2(3/2rule_thickness, 90);
  penpos3(1/2rule_thickness, 0);

  x1 = 2.5u;
  y1r = 40/45h;
  x2 = w - x1;
  y2r = y1r;
  rt x3 = w - u;
  y2l - y3 = y0 - y1r;
  x4 = x3;

  penpos12(rule_thickness/cosd 45, 45);
  penpos11(rule_thickness/cosd 45, 45);

  y11r = y12r = y4;
  bot y11l = 0;
  x12 = u;
  x11 = w - x12;

  filldraw z0{down} .. tension .8 .. z1l{right} .. {right}z2l .. {down}z3l --
          z3r{up} .. tension .8 .. z2r{left} .. {left}z1r .. z0 .. cycle;

  pickup rule_pen;
  draw z3 .. z4;

  filldraw z11l{left} .. {left}z12l -- z12r{right} .. {right}z11r -- cycle;
  labels(0,1l,1r,2l,2r,3l,3r,4,11l,11r,12l,12r);
endchar;

beginsymbol(7u#, asc_height#, 0); "gimel";

  pickup rule_pen;
  pickup currentpen scaled 1/2;

  top y0 = h;
  lft x0 = u;

  penpos1(3/2rule_thickness, 90);
  penpos2(3/2rule_thickness, 90);
  penpos3(1/2rule_thickness, 0);

  x1 = 2.5u;
  y1r = 40/45h;
  x2 = w - x1;
  y2r = y1r;
  x3 = w - 3/2u;
  y2l - y3 = y0 - y1r;

  filldraw z0{down} .. tension .8 .. z1l{right} .. {right}z2l .. {down}z3l --
          z3r{up} .. tension .8 .. z2r{left} .. {left}z1r .. z0 .. cycle;

  x4 = x3 + rule_thickness;
  y4 = 0;
  x5 = x4 + .3rule_thickness;
  x3a = x3b = x3c = x3;
  y3a = .8h;
  y3b = .5h;
  y3c = h/4;
  y5 := 1/2y3c;

  path p, q;

  p = (z3r -- z3l --- z3c .. z4);
  q = (z4{1, 1} .. z5 .. {-.005,1}z3r);

  filldraw p & q & cycle;

  penpos12(rule_thickness/cosd 60, 60);
  penpos11(rule_thickness/cosd 60, 60);

  x12r = u;
  y12l = 0;
  y11  = y12;
  x11r = x3 - 1/2u;
  x13  = x3;
  y13  = 0.3h;

  filldraw z11l -- z12l -- z12r{right} .. {up}z13{down} .. z11r -- cycle;
  penlabels(1,2,3,4,5,10,11,12,13);
endchar;

beginsymbol(10u#, asc_height#, 0); "daleth";

  pickup rule_pen;
  pickup currentpen scaled 1/2;

  top y0 = h;
  lft x0 = u;

  penpos1(3/2rule_thickness, 90);
  penpos2(3/2rule_thickness, 90);

  x1 = 2.5u;
  x2 = w - x1 + .5u;
  y2r = y1r = 10/11h;

  filldraw z0{down} .. tension .8 .. z1l{right} .. {right}z2l .. tension .8 .. {left}z2r
    .. {left}z1r .. z0 .. cycle;

  z3 = z2l;
  x4 = x3 + .2rule_thickness;
  y4 = 0;
  x5 = x4 + .3rule_thickness;
  y5 = y3c;
  x3a = x3b = x3c = x3;
  y3a = .8h;
  y3b = .5h;
  y3c = rule_thickness;
  y5 := .5y5; % reset y5

  path p, q;
  p = (z3 --- z3c .. z4);
  q = (z4{1,1} .. z5 .. {-.005,1}z3); % vertical

  filldraw p & q & cycle;
  labels(0,1l,1r,2l,2r,3,4,5);
endchar;


begingroup;

save slant;
save currenttransform;

slant := 0.212557;

transform currenttransform;

currenttransform := identity slanted slant yscaled aspect_ratio scaled granularity shifted (-u,0);

beginsymbol(11u#, x_height#, 8/25asc_height#);
  "Weierstrass p";

  pickup pencircle scaled 1/2rule_thickness;

  numeric theta;
  theta = 90-angle(8u,h);
  slope := -h/8u;  % angle at middle
  superness := 1/sqrt 2;
  superpull := 1/6;

  penpos2(1/2rule_thickness, -90);
  penpos1(5/6rule_thickness,theta);
  penpos7(1/2rule_thickness, -90);

  x2 + .5u = x1 = x7 - .5u = 3u;
  top y2l = h;
  bot y7r = -d;
  y1 = .4[-d, h];

  lft x3l = hround .5u;
  rt  x6r = hround max(rt x7 + .5, .5w - .25u);

  x3r - x3l = x6r - x6l = (hround 3/4rule_thickness) - 1/10rule_thickness;

  ellipse_set(2l,3l,4l,1l);
  ellipse_set(2r,3r,4r,1r);
  ellipse_set(7l,6l,5l,1l);
  ellipse_set(7r,6r,5r,1r);

  y3 = y3r;
  y6 = y6r;

  filldraw stroke super_arc.e(2,3) & z3e{down} .. z4e --- z5e .. z6e{down} & super_arc.e(6,7);  % flourish

  penpos8(1/2rule_thickness, 180);
  penpos9(1/2rule_thickness, 90);
  penpos10(rule_thickness, 0);
  penpos11(1/2rule_thickness, -90);
  penpos12(1/2rule_thickness, -180);
  penpos13(5/7rule_thickness, -180);

  rt  x10r = hround(w - 1.5u + 1/2rule_thickness);
  lft x12r = hround(.5w - .5u);
  rt  x8l  = hround 2.25u;
  x9 = x11 = max(x12l + eps, .5w + 1.5u);
  y8 = 0;
  top y9r = vround .95h;
  y10 = .5[y9,y11];
  bot y11r = y8;

  filldraw stroke z7e{left} ... z8e{up} ... pulled_arc.e(9,10) & pulled_arc.e(10,11);  % stem and bowl

  y13 - 5/7rule_thickness = .15h;
  bulb(11,12,13);  % bulb

  penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

beginsymbol(11u#, 11/10asc_height#, 1/10asc_height#);
  "powerset";

  pickup pencircle scaled 1/2rule_thickness;

  numeric theta;
  theta = 90 - angle(12/7*8u, h);
  slope := -h/(12/7*8u);  % angle at middle
  superness := 1/sqrt 2;
  superpull := 1/6;

  penpos2(1/2rule_thickness, -90);
  penpos1(rule_thickness,theta);
  penpos7(1/2rule_thickness, -90);

  x2 + .5u = x1 = x7 - .5u = 3u;
  top y2l = h;
  bot y7r = -d;
  y1 = .4h;

  lft x3l = hround .5u;
  rt  x6r = hround max(rt x7 + .5, .5w - .25u);

  x3r - x3l = x6r - x6l = (hround 3/4rule_thickness) - 1/10rule_thickness;

  ellipse_set(2l,3l,4l,1l);
  ellipse_set(2r,3r,4r,1r);
  ellipse_set(7l,6l,5l,1l);
  ellipse_set(7r,6r,5r,1r);

  y3 = y3r;
  y6 = y6r;

  filldraw stroke super_arc.e(2,3) & z3e{down} .. z4e --- z5e .. z6e{down} & super_arc.e(6,7);  % flourish

  penpos8(1/2rule_thickness, 180);
  penpos9(1/2rule_thickness, 90);
  penpos10(rule_thickness, 0);
  penpos11(1/2rule_thickness, -90);
  penpos12(1/2rule_thickness, -180);
  penpos13(5/7rule_thickness, -180);

  rt  x10r = hround(w - 1.5u + 1/2rule_thickness);
  lft x12r = hround(.5w - .5u);
  rt  x8l  = hround 2.25u;
  x9 = x11 = max(x12l + eps, .5w + 1.5u);
  y8 = 10/11*5/12h;
  top y9r = vround .95h;
  y10 = .5[y9,y11];
  bot y11r = y8;

  filldraw stroke z7e{left} ... z8e{up} ... pulled_arc.e(9,10) & pulled_arc.e(10,11);  % stem and bowl

  y13 - 5/7rule_thickness = y8 + 1/10h;
  bulb(11,12,13);  % bulb

  penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
endchar;

endgroup;

