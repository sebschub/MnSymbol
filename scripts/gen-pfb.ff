#!/usr/local/bin/fontforge
names=["A", "B", "C", "D", "E", "F", "S", "A-Bold", "B-Bold", "C-Bold", "D-Bold", "E-Bold", "F-Bold", "S-Bold"];

postprocess=0

size=5;
while (size<=12)
fname=0;
while (fname<=13)
  Print("MnSymbol"+names[fname]+size);
  Open("MnSymbol"+names[fname]+size+".pfb");
  widths = Array(CharCnt());
  SelectAll();
  ii=0;
  foreach
    widths[ii] = GlyphInfo("Width");
    ii++;
  endloop
  Clear();
  ii=0;
  while(ii<=255)
    Select(ii);
    Import("MnSymbol"+names[fname]+size+"."+ii+".eps");
    SetWidth(widths[ii]);
    ii++;
  endloop
  SelectAll();
  DontAutoHint();
  Generate("MnSymbol"+names[fname]+size+"_new.pfb","",0);
  fname++;
endloop
  Print("Merging MnSymbol"+size);
  Open("MnSymbolA"+size+"_new.pfb");
  MergeFonts("MnSymbolB"+size+"_new.pfb");
  MergeFonts("MnSymbolC"+size+"_new.pfb");
  MergeFonts("MnSymbolD"+size+"_new.pfb");
  MergeFonts("MnSymbolE"+size+"_new.pfb");
  MergeFonts("MnSymbolF"+size+"_new.pfb");
  MergeFonts("MnSymbolS"+size+"_new.pfb");
  SetFontNames("MnSymbol"+size,"MnSymbol","MnSymbol"+size,"Regular","Design by Achim Blumensath, Conversion to pfb by Michael Zedler","1.001");
  SelectAll();
  if (postprocess==1)
    RemoveOverlap();
    RoundToInt();
    AddExtrema();
    Simplify();
    AutoHint();
  else
    DontAutoHint();
  endif
  Generate("MnSymbol"+size+".pfb", "", 0);

  Print("Merging MnSymbol-Bold"+size);
  Open("MnSymbolA-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolB-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolC-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolD-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolE-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolF-Bold"+size+"_new.pfb");
  MergeFonts("MnSymbolS-Bold"+size+"_new.pfb");
  SetFontNames("MnSymbol-Bold"+size,"MnSymbol","MnSymbol-Bold"+size,"Bold","Design by Achim Blumensath, Conversion to pfb by Michael Zedler","1.001");
  ChangePrivateEntry("ForceBold","true");
  SelectAll();
  if (postprocess==1)
    RemoveOverlap();
    RoundToInt();
    AddExtrema();
    Simplify();
    AutoHint();
  else
    DontAutoHint();
  endif
  Generate("MnSymbol-Bold"+size+".pfb", "", 0);

  size++;
  if (size==11)
    size++;
  endif
endloop

